// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
    //// Replace this comment with your code.
    // zx
    Mux16(a= x, b= false, sel= zx , out= zxo);

    //nx
    Not16(in = zxo, out =notx);
    Mux16(a= zxo, b= notx, sel= nx, out= nxo);

    //zy
    Mux16(a= y, b= false, sel= zy , out= zyo);

    //ny
    Not16(in = zyo, out =noty);
    Mux16(a= zyo, b= noty, sel= ny, out= nyo);

    //f
    Add16(a= nxo, b= nyo, out= fplus);
    And16(a= nxo, b= nyo, out= fand);
    Mux16(a= fand, b= fplus, sel= f , out= fo);

    //no
    Not16(in=fo, out = negfo);
    Mux16(a=fo , b= negfo, sel= no, out= out, out[0..7]= o1, out[8..15] =o2, out[15] = n1);

    //zr
    Or8Way(in= o1, out= zro1);
    Or8Way(in= o2, out= zro2);
    Or(a= zro1, b= zro2, out= z1);
    Mux(a=true, b= false, sel= z1, out= zr);

    //ng
    Mux(a=false, b= true, sel= n1, out= ng);


    






}